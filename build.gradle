import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.4"
    }
}

group 'cloud.swiftnode.ksecurity'
version '3.2.1'

apply plugin: 'java'
apply plugin: "com.github.johnrengelman.shadow"

test.testLogging.showStandardStreams = true
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
compileJava.dependsOn clean

sourceCompatibility = 1.8

repositories {
    /* Spigot Repo */
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    /* Bungeecord Repo */
    maven { url 'https://oss.sonatype.org/content/groups/public' }
    mavenCentral()
}

dependencies {
    compile 'org.spigotmc:spigot-api:1.11-R0.1-SNAPSHOT'
    compile 'org.ow2.asm:asm:5.2'
    compile 'cglib:cglib:3.2.4'
    compile files('libs/TrayNotification.jar')
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

processResources {
    filteringCharset = 'UTF-8'
    filter ReplaceTokens, tokens: [
            "version"   : project.version,
            "pluginName": rootProject.name,
            "mainClass" : "cloud.swiftnode.ksecurity.KSecurity",
            "author"    : "EntryPoint"
    ]
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

shadowJar {
    classifier = ''
    dependencies {
        include dependency('org.ow2.asm:asm')
        include dependency('cglib:cglib')
    }
}

artifacts {
    archives shadowJar
}
